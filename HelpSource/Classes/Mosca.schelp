TITLE:: Mosca
summary:: GUI-assisted production of dynamic ambisonic sound fields
categories:: Undocumented classes
related:: Classes/SomeRelatedClass, http://www.ambisonictoolkit.net

DESCRIPTION::
Mosca is a SuperCollider class for GUI-assisted production of ambisonic sound fields with simulated moving or stationary sound sources. Sound fields may be decoded using a variety of built in 1st order ambisonic SuperCollider decoders (including binaural) or with external 2nd order decoders such as Ambdec in Linux. The class makes extensive use of the Ambisonic Toolkit (ATK, see: http://www.ambisonictoolkit.net/) by Joseph Anderson and the Automation quark (https://github.com/neeels/Automation) by Neels Hofmeyr.

Input sources may be any combination of mono, stereo or B-format material and the signals may originate from file, from hardware inputs (physical or from other applications such a DAW via Jack) or from SuperCollider's own synths. In the case of synth input, synths are associated by the user with a particular source in the GUI and registered in a synth registry. In this way, they are spatialised by the GUI and also receive all of the data from the GUI pertaining to the source (eg. x, y and z coordinates or auxiliary fader data). Mosca has its own transport provided by the Automation quark for recording and playback of source data. This may be used independently or may be synchronised to a DAW using Midi Machine Control (MMC) messages. This function has been tested to work with Ardour and Jack.

Mono and stereo sources are encoded as second order ambisonic signals whereas B-format signals remain as 1st order and are angled in space using "push" transformations. Source signals are attenuated proportionally to the inverse of the square root of proximity or in a linear relationship with distance, selectable on a per-source basis via the GUI. All sources are subject to high-frequency attenuation with distance and if decoding is performed by one of the ATK's 1st order decoders, a proximity effect is generated adding a bass boost to proximal sources among other phase effects to simulate wave curvature (see: http://doc.sccode.org/Classes/FoaProximity.html).

Reverberation is performed either using a B-format tail room impulse response (RIR) - the preferred method - or using simple built-in allpass filters, options selectable on creation of a Mosca instance. With both options, two reverberation level controls are included in the GUI to set proximal and distant levels. A further two reverb types are selectable in the GUI on a per-source basis for both RIR and allpass reverberation modes. The default reverb type uses John Chowning's technique of applying "local" and "global" reverberation to sources. Proximal reverberation in this case is "global" and is audible by the listener from all directions when the source is close where as distant reverb is "local" in scope and is encoded as a 2nd order ambisonic signal along with the dry signal. This predominates as the source becomes more distant. The second type of reverberation may be described as a "2nd order diffuse reverberation". This technique produces reverberation weighted in the direction of sound events encoded in the dry ambisonic signal. The encoded 2nd order ambisonic signal is converted to a 12-channel A-format signal and then either a) convolved with a B-format RIR which has been "upsampled" to 2nd order and converted to A-format impulse spectrum, or, as in the case of the allpass option, b) passed through a 12-channel bank of allpass filters before being converted back to a 2nd order B-format diffuse signal. Please note that the 2nd order diffuse reverberation may require the user to set a larger audio output buffer and thus increase the latency of the system. The "Chowning" type reverberation is more efficient and the "allpass" option, more still. 

Mosca also has other features including a scalable Doppler Effect on moving sources, looping of sources loaded from file, adjustment of virtualloudspeaker angle of stereo sources and in the case of B-format sources: a rotation control, adjustment of "directivity" (see ATK documentation) and a control of "contraction", whereby the B-format signal may be crossfaded with its W component and which is spatialised as a 2nd order ambisonic signal.

USING MOSCA

The user must set up a project directory with subdirectories "rir" and "auto". RIRs should have the first 100 or 120ms silenced to act as "tail" reverberator and must be placed in the "rir" directory. For convenience, please download the "moscaproject.zip" file on the following page which contains the file structure, example RIRs and B-format recordings as well as other information and demos:

http://escuta.org/mosca

Please then see the methods and code examples below.


CLASSMETHODS::

METHOD:: new
Create Mosca instance, prepare RIR spectrum data and synth internals.

ARGUMENT:: projDir
Path to project directory. The project directory must have 2 subdirectories named "rir" and "auto". The first contains B-format RIRs to be used for reverberation and the second may be initially empty.

ARGUMENT:: nsources
The number of sources to be spatialised.

ARGUMENT:: rirWXYZ
B-Format ambisonic room impulse response (contained in projDir/rir). Ensure that it has the same sample rate as your system.

ARGUMENT:: srvr
Server used. eg. Server.default

ARGUMENT:: decoder
Name of 1st order ambisonic decoder to be used. See the following ATK links:
http://doc.sccode.org/Classes/FoaDecode.html http://doc.sccode.org/Classes/FoaDecoderMatrix.html http://doc.sccode.org/Classes/FoaDecoderKernel.html Note. If decoder is left blank, Mosca will send raw 1st order and 2nd order signals to the outputs for external decoding with, for example, AmbDec: http://kokkinizita.linuxaudio.org/linuxaudio/

returns:: New instance.


METHOD:: printSynthParams
Print GUI Parameters usable in SynthDefs.


returns:: A string.

INSTANCEMETHODS::

METHOD:: openGui
Creates GUI window

ARGUMENT:: dur
Duration in seconds of the "Automation" transport.

returns:: (describe returnvalue here)

METHOD:: registerSynth
Registers synth instance for a given source so that it may be spatialised and receive control data from GUI.

ARGUMENT:: source
The number of the source (starting with 1).

ARGUMENT:: synth
Synth instance.

returns:: (describe returnvalue here)

METHOD:: deregisterSynth
Deregisters synth instance for a given source.

ARGUMENT:: source
The number of the source (starting with 1).

ARGUMENT:: synth
Synth instance.

returns:: (describe returnvalue here)

METHOD:: getSWBus
Return the "real" bus number associated with synth registrant.

ARGUMENT:: source
The number of the source (starting with 1). See code example below.

returns:: (describe returnvalue here)



EXAMPLES::

code::



/*
Please also read "USING MOSCA" above

Make sure the server is stopped before running this block example code
*/

(
s = Server.local;
o = s.options;
o.numAudioBusChannels = 1024;
o.numInputBusChannels = 32;
o.numOutputBusChannels = 11; // 2 for stereo & 9 for 2nd order ambisonics
o.memSize = 64 * 8192;
o.numAudioBusChannels = 512;
o.numWireBufs = 512;

s.waitForBoot {

// Add a SC internal decoder (1º only)
// See the ATK docs for more info: http://doc.sccode.org/Classes/FoaDecode.html
//~decoder = FoaDecoderKernel.newUHJ;
~decoder = FoaDecoderKernel.newCIPIC(21); // Binaural
s.sync;
MIDIIn.connect;
s.sync;
//create a project directory and in it create to more directories "rir" and "auto".
//place your binaural and ambisonic rirs in the rir folder. For some demo rirs
//and ambisonic recordings see the zip archive
//here: http://escuta.org/mosca 

// Mosca.new(<project directory path><b-format rir><binaural rir><server><decoder>)
//If <decoder> is left blank, Mosca will send 2ndº and 1º signals out of SC's
// outputs for decoding with an external decoder

~testMosca = Mosca.new("/home/iain/projects/ambisonics/moscaproject", 12, 950,
"QL14Tail2Sec.amb", s, ~decoder);
//~testMosca = Mosca.new("/home/iain/projects/ambisonics/moscaproject", 12, 950,
//"allpass", s, ~decoder);

s.sync;
~testMosca.openGui(120);

};

)



// If you close the Mosca window, tidy up

(
MIDIIn.disconnect; 
~decoder.free;
)

// "SW input". Note. Must have SW Input selected on GUI for particular source.

// write a synth def

(
SynthDef("test-out", { arg outbus=0, freq=440, mx, my;
	var source, point, adjust, delEnv;
	point = Point.new;
	point.set(mx, my);
	adjust = point.rho;
	adjust = Lag.kr(adjust, 0.1);
	delEnv = Env.dadsr(0.1, attackTime: 0.01, decayTime: 200,
	sustainLevel: 0.1); // envelope with an onset delay equal to lag buffer
	source = SinOsc.ar(freq + adjust, mul: EnvGen.kr(delEnv, doneAction: 2));

	// write to the bus, adding to previous contents
	Out.ar(outbus, source);

}).send(s);
)

//  use the method to fetch the true bus associated with the chosen source (from 1 on). Note
// this depends on the setting in the GUI. Use the value for the outbus of your synth instance.

// ~testMosca.getSCBus(1) // eg. get bus for source 2

// To see the GUI parameters available in registered synths, run:

Mosca.printSynthParams

(
~source1PlayFunc = {
~mySynth = Synth.new("test-out", [\freq, 500, \outbus, ~testMosca.getSCBus(1)]).onFree({"done".postln; ~testMosca.deregisterSynth(1, ~mySynth)});

~testMosca.registerSynth(1, ~mySynth);
};
~source1StopFunc = {~mySynth.free; };
~testMosca.setTriggerFunc(1, ~source1PlayFunc); 
~testMosca.setStopFunc(1, ~source1StopFunc);
)

(
~source2PlayFunc = {
~myOtherSynth = Synth.new("test-out", [\freq, 500, \outbus, ~testMosca.getSCBus(2)]).onFree({"done".postln; ~testMosca.deregisterSynth(2, ~myOtherSynth)});

~testMosca.registerSynth(2, ~myOtherSynth);
};
~source2StopFunc = {~myOtherSynth.free; };
~testMosca.setTriggerFunc(2, ~source2PlayFunc); 
~testMosca.setStopFunc(2, ~source2StopFunc);
)



::